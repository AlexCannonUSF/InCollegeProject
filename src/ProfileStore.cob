IDENTIFICATION DIVISION.
PROGRAM-ID. ProfileStore.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT PROFILE-FILE ASSIGN TO "data/profiles.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS WS-FILE-CODE.

DATA DIVISION.
FILE SECTION.
FD PROFILE-FILE.
01 PF-REC.
    05 PF-USERNAME    PIC X(30).
    05 PF-NAME        PIC X(50).
    05 PF-UNIVERSITY  PIC X(50).
    05 PF-MAJOR       PIC X(50).
    05 PF-GRADYEAR    PIC 9(4).
    05 PF-ABOUT       PIC X(200).
    05 PF-JOBTITLE    OCCURS 3 TIMES PIC X(50).
    05 PF-COMPANY     OCCURS 3 TIMES PIC X(50).
    05 PF-DATES       OCCURS 3 TIMES PIC X(30).
    05 PF-DESC        OCCURS 3 TIMES PIC X(200).
    05 PF-DEGREE      OCCURS 3 TIMES PIC X(50).
    05 PF-UNIV        OCCURS 3 TIMES PIC X(50).
    05 PF-YEARS       OCCURS 3 TIMES PIC X(30).

WORKING-STORAGE SECTION.
77 WS-FILE-CODE   PIC XX VALUE "00".
77 WS-EOF         PIC X  VALUE "N".
77 WS-I           PIC 9  VALUE 0.
77 WS-MAX         PIC 9  VALUE 5.

LINKAGE SECTION.
77 LK-ACTION       PIC X.
77 LK-PROFILE-COUNT PIC 9.
01 LK-PROFILE-LIST.
    05 LK-PROF-ROW OCCURS 5 TIMES.
        10 LK-USERNAME    PIC X(30).
        10 LK-NAME        PIC X(50).
        10 LK-UNIVERSITY  PIC X(50).
        10 LK-MAJOR       PIC X(50).
        10 LK-GRADYEAR    PIC 9(4).
        10 LK-ABOUT       PIC X(200).
        10 LK-JOBTITLE    OCCURS 3 TIMES PIC X(50).
        10 LK-COMPANY     OCCURS 3 TIMES PIC X(50).
        10 LK-DATES       OCCURS 3 TIMES PIC X(30).
        10 LK-DESC        OCCURS 3 TIMES PIC X(200).
        10 LK-DEGREE      OCCURS 3 TIMES PIC X(50).
        10 LK-UNIV        OCCURS 3 TIMES PIC X(50).
        10 LK-YEARS       OCCURS 3 TIMES PIC X(30).

PROCEDURE DIVISION USING LK-ACTION LK-PROFILE-COUNT LK-PROFILE-LIST.

MAIN.
    EVALUATE LK-ACTION
        WHEN "L" PERFORM LOAD-PROFILES
        WHEN "S" PERFORM SAVE-PROFILES
        WHEN OTHER CONTINUE
    END-EVALUATE
    GOBACK.

LOAD-PROFILES.
    MOVE 0 TO LK-PROFILE-COUNT
    MOVE "N" TO WS-EOF
    OPEN INPUT PROFILE-FILE
    IF WS-FILE-CODE NOT = "00"
        CLOSE PROFILE-FILE
        EXIT PARAGRAPH
    END-IF
    PERFORM UNTIL WS-EOF = "Y" OR LK-PROFILE-COUNT = WS-MAX
        READ PROFILE-FILE
            AT END MOVE "Y" TO WS-EOF
            NOT AT END
                ADD 1 TO LK-PROFILE-COUNT
                MOVE PF-REC TO LK-PROF-ROW(LK-PROFILE-COUNT)
        END-READ
    END-PERFORM
    CLOSE PROFILE-FILE.

SAVE-PROFILES.
    OPEN OUTPUT PROFILE-FILE
    PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > LK-PROFILE-COUNT
        MOVE LK-PROF-ROW(WS-I) TO PF-REC
        WRITE PF-REC
    END-PERFORM
    CLOSE PROFILE-FILE.