>>SOURCE FORMAT FREE
IDENTIFICATION DIVISION.
PROGRAM-ID. CreateAccount.

DATA DIVISION.
WORKING-STORAGE SECTION.

77 WS-LIMIT-ACCOUNTS PIC 9 VALUE 5.
77 WS-TRY-COUNT PIC 99 VALUE 0.
77 WS-SCAN-INDEX PIC 99 VALUE 0.
77 WS-TEMP-USERNAME PIC X(30) VALUE SPACES.
77 WS-TEMP-PASSWORD PIC X(12) VALUE SPACES.
77 WS-USERNAME-LEN PIC 99 VALUE 0.
77 WS-PASSWORD-LEN PIC 99 VALUE 0.
77 WS-NAME-ALREADY-USED PIC X VALUE "N".
77 WS-PASS-IS-GOOD PIC X VALUE "N".
77 WS-FLAG-UPPERCASE PIC X VALUE "N".
77 WS-FLAG-HAS-DIGIT PIC X VALUE "N".
77 WS-FLAG-SPECIAL-CHAR PIC X VALUE "N".
77 WS-CURRENT-CHAR PIC X VALUE SPACE.
LINKAGE SECTION.
77 LK-STORED-COUNT PIC 9.
01 LK-CREDENTIALS-LIST.
05 LK-CREDS-ROW OCCURS 5 TIMES.
10 LK-STORED-USERNAME PIC X(30).
10 LK-STORED-PASSWORD PIC X(12).
PROCEDURE DIVISION USING LK-STORED-COUNT LK-CREDENTIALS-LIST.

SIGNUP-FLOW.
    IF LK-STORED-COUNT = WS-LIMIT-ACCOUNTS
        DISPLAY "All permitted accounts have been created, please come back later"
        GOBACK
    END-IF
    PERFORM PROMPT-USERNAME
    IF FUNCTION TRIM(WS-TEMP-USERNAME) = ""
        GOBACK
    END-IF
    PERFORM PROMPT-PASSWORD
    IF FUNCTION TRIM(WS-TEMP-PASSWORD) = ""
        GOBACK
    END-IF
    PERFORM STORE-NEW-CREDS
    DISPLAY "Account created successfully!"
    GOBACK.

PROMPT-USERNAME.
    MOVE "Y" TO WS-NAME-ALREADY-USED
    MOVE 0 TO WS-USERNAME-LEN
    MOVE 0 TO WS-TRY-COUNT
    PERFORM UNTIL WS-NAME-ALREADY-USED = "N" AND WS-USERNAME-LEN > 0
        ADD 1 TO WS-TRY-COUNT
        IF WS-TRY-COUNT > 10
            DISPLAY "Too many attempts. Returning to menu."
            MOVE SPACES TO WS-TEMP-USERNAME
            EXIT PARAGRAPH
        END-IF
        DISPLAY "Enter a username: "
        ACCEPT WS-TEMP-USERNAME
        DISPLAY WS-TEMP-USERNAME
        COMPUTE WS-USERNAME-LEN = FUNCTION LENGTH(FUNCTION TRIM(WS-TEMP-USERNAME))
        IF WS-USERNAME-LEN = 0
            DISPLAY "Username cannot be blank."
            MOVE "Y" TO WS-NAME-ALREADY-USED
        ELSE
            PERFORM CHECK-USERNAME-TAKEN
            IF WS-NAME-ALREADY-USED = "Y"
                DISPLAY "That username is already taken."
            END-IF
        END-IF
    END-PERFORM.

CHECK-USERNAME-TAKEN.
    MOVE "N" TO WS-NAME-ALREADY-USED
    IF LK-STORED-COUNT = 0
        EXIT PARAGRAPH
    END-IF
    PERFORM VARYING WS-SCAN-INDEX FROM 1 BY 1 UNTIL WS-SCAN-INDEX > LK-STORED-COUNT
        IF FUNCTION TRIM(LK-STORED-USERNAME(WS-SCAN-INDEX)) = FUNCTION TRIM(WS-TEMP-USERNAME)
            MOVE "Y" TO WS-NAME-ALREADY-USED
            EXIT PERFORM
        END-IF
    END-PERFORM.

PROMPT-PASSWORD.
    MOVE "N" TO WS-PASS-IS-GOOD
    MOVE 0 TO WS-TRY-COUNT
    PERFORM UNTIL WS-PASS-IS-GOOD = "Y"
        ADD 1 TO WS-TRY-COUNT
        IF WS-TRY-COUNT > 10
            DISPLAY "Too many attempts. Returning to menu."
            MOVE SPACES TO WS-TEMP-PASSWORD
            EXIT PARAGRAPH
        END-IF
        DISPLAY "Enter password: "
        ACCEPT WS-TEMP-PASSWORD
        DISPLAY WS-TEMP-PASSWORD
        COMPUTE WS-PASSWORD-LEN = FUNCTION LENGTH(FUNCTION TRIM(WS-TEMP-PASSWORD))
        IF WS-PASSWORD-LEN = 0
            DISPLAY "Password cannot be blank."
            MOVE "N" TO WS-PASS-IS-GOOD
        ELSE
            PERFORM VALIDATE-PASSWORD
            IF WS-PASS-IS-GOOD = "N"
                DISPLAY "Password does not meet requirements."
            END-IF
        END-IF
    END-PERFORM.

VALIDATE-PASSWORD.
    MOVE "N" TO WS-FLAG-UPPERCASE
    MOVE "N" TO WS-FLAG-HAS-DIGIT
    MOVE "N" TO WS-FLAG-SPECIAL-CHAR
    MOVE "N" TO WS-PASS-IS-GOOD
    COMPUTE WS-PASSWORD-LEN = FUNCTION LENGTH(FUNCTION TRIM(WS-TEMP-PASSWORD))
    IF WS-PASSWORD-LEN < 8 OR WS-PASSWORD-LEN > 12
        EXIT PARAGRAPH
    END-IF
    PERFORM VARYING WS-SCAN-INDEX FROM 1 BY 1 UNTIL WS-SCAN-INDEX > WS-PASSWORD-LEN
        MOVE WS-TEMP-PASSWORD(WS-SCAN-INDEX:1) TO WS-CURRENT-CHAR
        IF WS-CURRENT-CHAR >= "A" AND WS-CURRENT-CHAR <= "Z"
            MOVE "Y" TO WS-FLAG-UPPERCASE
        END-IF
        IF WS-CURRENT-CHAR >= "0" AND WS-CURRENT-CHAR <= "9"
            MOVE "Y" TO WS-FLAG-HAS-DIGIT
        END-IF
        EVALUATE WS-CURRENT-CHAR
            WHEN "!" WHEN "@" WHEN "#" WHEN "$" WHEN "%" WHEN "^" WHEN "&" WHEN "*" WHEN "?" WHEN "_" WHEN "-"
                MOVE "Y" TO WS-FLAG-SPECIAL-CHAR
            WHEN OTHER
                CONTINUE
        END-EVALUATE
    END-PERFORM
    IF WS-FLAG-UPPERCASE = "Y"
       AND WS-FLAG-HAS-DIGIT = "Y"
       AND WS-FLAG-SPECIAL-CHAR = "Y"
        MOVE "Y" TO WS-PASS-IS-GOOD
    END-IF.

STORE-NEW-CREDS.
    ADD 1 TO LK-STORED-COUNT
    MOVE WS-TEMP-USERNAME TO LK-STORED-USERNAME(LK-STORED-COUNT)
    MOVE WS-TEMP-PASSWORD TO LK-STORED-PASSWORD(LK-STORED-COUNT).