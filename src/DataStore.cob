>>SOURCE FORMAT FREE
IDENTIFICATION DIVISION.
PROGRAM-ID. DataStore.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT ACCT-FILE ASSIGN TO "data/accounts.dat"
        ORGANIZATION IS LINE SEQUENTIAL
        FILE STATUS IS WS-FILE-CODE.

DATA DIVISION.
FILE SECTION.
FD ACCT-FILE.
01 ACCT-LINE.
05 ACCT-LINE-USERNAME PIC X(30).
05 ACCT-LINE-PASSWORD PIC X(12).
WORKING-STORAGE SECTION.
77 WS-FILE-CODE PIC XX VALUE "00".
77 WS-HIT-END PIC X VALUE "N".
77 WS-SLOT-INDEX PIC 9 VALUE 0.
77 WS-MAX-SLOTS PIC 9 VALUE 5.
LINKAGE SECTION.
77 LK-ACTION PIC X.
77 LK-STORED-COUNT PIC 9.
01 LK-CREDENTIALS-LIST.
05 LK-CREDS-ROW OCCURS 5 TIMES.
10 LK-STORED-USERNAME PIC X(30).
10 LK-STORED-PASSWORD PIC X(12).

PROCEDURE DIVISION USING LK-ACTION LK-STORED-COUNT LK-CREDENTIALS-LIST.
DATASTORE-FLOW.
    EVALUATE LK-ACTION
        WHEN "L"
            PERFORM LOAD-CREDENTIALS
        WHEN "S"
            PERFORM SAVE-CREDENTIALS
        WHEN OTHER
            CONTINUE
    END-EVALUATE
    GOBACK.

LOAD-CREDENTIALS.
    MOVE 0   TO LK-STORED-COUNT
    MOVE "N" TO WS-HIT-END
    OPEN INPUT ACCT-FILE
    IF WS-FILE-CODE NOT = "00"
        CLOSE ACCT-FILE
        EXIT PARAGRAPH
    END-IF
    PERFORM UNTIL WS-HIT-END = "Y" OR LK-STORED-COUNT = WS-MAX-SLOTS
        READ ACCT-FILE
            AT END
                MOVE "Y" TO WS-HIT-END
            NOT AT END
                ADD 1 TO LK-STORED-COUNT
                MOVE ACCT-LINE-USERNAME TO LK-STORED-USERNAME(LK-STORED-COUNT)
                MOVE ACCT-LINE-PASSWORD TO LK-STORED-PASSWORD(LK-STORED-COUNT)
        END-READ
    END-PERFORM
    CLOSE ACCT-FILE.

SAVE-CREDENTIALS.
    OPEN OUTPUT ACCT-FILE
    PERFORM VARYING WS-SLOT-INDEX FROM 1 BY 1 UNTIL WS-SLOT-INDEX > LK-STORED-COUNT
        MOVE LK-STORED-USERNAME(WS-SLOT-INDEX) TO ACCT-LINE-USERNAME
        MOVE LK-STORED-PASSWORD(WS-SLOT-INDEX) TO ACCT-LINE-PASSWORD
        WRITE ACCT-LINE
    END-PERFORM
    CLOSE ACCT-FILE.