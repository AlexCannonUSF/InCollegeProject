>>SOURCE FORMAT FREE
IDENTIFICATION DIVISION.
PROGRAM-ID. ViewRequests.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
       SELECT PENDING-REQUESTS-FILE ASSIGN TO "data/PendingRequests.dat"
           ORGANIZATION IS SEQUENTIAL
           ACCESS MODE IS SEQUENTIAL
           FILE STATUS IS LS-STAT.

DATA DIVISION.
FILE SECTION.
FD PENDING-REQUESTS-FILE.
01 PENDING-REQUESTS-RECORD.
       05 SENDER-USERNAME PIC X(30).
       05 RECIPIENT-USERNAME PIC X(30).

LOCAL-STORAGE SECTION.
01 LS-STAT PIC XX.
01 LS-EOF  PIC X  VALUE "N".
01 LS-MAX_REQUESTS PIC 99 VALUE 25.
01 LS-REQUEST-COUNT PIC 99 VALUE 0.
01 RECEIVED-REQUESTS PIC X(30) OCCURS 0 TO 25 TIMES DEPENDING ON LS-REQUEST-COUNT.
01 REQUEST-INDEX PIC 99.
01 LS-NO-LEADING-ZEROS PIC ZZ.

LINKAGE SECTION.
01 LNK-USER-NAME PIC X(30).

PROCEDURE DIVISION USING LNK-USER-NAME.

MAIN.
       DISPLAY "--- Pending Connection Requests ---"
       OPEN I-O PENDING-REQUESTS-FILE

       EVALUATE LS-STAT
           WHEN "00"
               CONTINUE
           WHEN "35"
               CLOSE PENDING-REQUESTS-FILE
               OPEN OUTPUT PENDING-REQUESTS-FILE
               CLOSE PENDING-REQUESTS-FILE
               OPEN I-O PENDING-REQUESTS-FILE
           WHEN OTHER
               DISPLAY "Error Opening PendingRequests.dat: " LS-STAT
               CLOSE PENDING-REQUESTS-FILE
               GOBACK
       END-EVALUATE

       PERFORM UNTIL LS-EOF = "Y" OR LS-REQUEST-COUNT >= LS-MAX_REQUESTS
           READ PENDING-REQUESTS-FILE
               AT END MOVE "Y" TO LS-EOF
               NOT AT END
                   IF LNK-USER-NAME = RECIPIENT-USERNAME
                       ADD 1 TO LS-REQUEST-COUNT
                       MOVE SENDER-USERNAME TO RECEIVED-REQUESTS(LS-REQUEST-COUNT)
      *>                 DELETE PENDING-REQUESTS-FILE
                   END-IF 
           END-READ
       END-PERFORM

       CLOSE PENDING-REQUESTS-FILE
       IF LS-REQUEST-COUNT = 0
           DISPLAY "You have no pending connection requests at this time."
           DISPLAY "-----------------------------------"
           GOBACK
       END-IF
       
       DISPLAY "People who want to connect:"
       PERFORM VARYING REQUEST-INDEX FROM 1 BY 1 UNTIL REQUEST-INDEX > LS-REQUEST-COUNT
           MOVE REQUEST-INDEX TO LS-NO-LEADING-ZEROS
           DISPLAY FUNCTION TRIM(LS-NO-LEADING-ZEROS) ". " FUNCTION TRIM(RECEIVED-REQUESTS(REQUEST-INDEX))
       END-PERFORM
       DISPLAY "-----------------------------------"
       GOBACK.
